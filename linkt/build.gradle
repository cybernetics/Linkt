plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id("maven-publish")
    id("com.jfrog.bintray") version "1.8.5"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}


task androidSourcesJar(type: Jar) {
    getArchiveClassifier().set('sources')
    from android.sourceSets.main.java.srcDirs
}

def publishVersion = '0.1.0'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'org.linkt'
                artifactId = 'linkt'
                version = publishVersion
                artifact androidSourcesJar
            }
        }
    }
}

if (hasProperty("user") && hasProperty("key")) {
    bintray {
        user = property("user")
        key = property("key")
        setPublications("release")
        pkg {
            name = "Linkt"
            repo = "Linkt"
            websiteUrl = "https://github.com/jeziellago/Linkt"
            issueTrackerUrl = "https://github.com/jeziellago/Linkt/issues"
            vcsUrl = "https://github.com/jeziellago/Linkt.git"
            publicDownloadNumbers = true
            setLicenses("MIT")
            desc = "A simple kotlin library for deep link handling easy on Android"
            version.name = publishVersion
            version.vcsTag = publishVersion
            publish = true
            override = true
        }
    }
}